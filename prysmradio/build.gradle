apply plugin: 'android'

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        versionCode Integer.parseInt(project.VERSION_CODE)
        versionName project.VERSION_NAME
    }

    sourceSets.main {
        jniLibs.srcDir 'libs'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    signingConfigs {
        release {
            storeFile file(project.KEY_STORE_FILE)
            storePassword project.KEY_STORE_PASSWORD
            keyAlias project.KEY_STORE_KEY_ALIAS
            keyPassword project.KEY_STORE_KEY_PASSWORD
        }
    }

    lintOptions {
        abortOnError false
    }

    productFlavors {
        flavorFR {
            packageName "com.prysmradio"
        }

        flavorUK {
            packageName "uk.co.prysmradio"
        }
    }

    buildTypes {

        debug {
            signingConfig signingConfigs.debug
            debuggable true
            zipAlign true
        }

        release {
            signingConfig signingConfigs.release
            buildConfigField "boolean", "IS_BUG_TRACKER_ENABLED", "false"
            buildConfigField "boolean", "IS_ANALYTICS_ENABLED", "true"
            debuggable false
            zipAlign true
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:19.1.0'
    compile 'com.android.support:appcompat-v7:19.0.0'
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.jakewharton:butterknife:4.0.+'
    compile 'com.squareup:otto:1.3.4'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.1'
    compile 'de.keyboardsurfer.android.widget:crouton:1.8.+'
    compile 'com.octo.android.robospice:robospice-retrofit:1.4.12'
    compile files('libs/aacdecoder-android-0.8.jar')
    compile 'com.squareup.picasso:picasso:2.3.3'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.daimajia.slider:library:1.0.9@aar'
    compile project(':facebook')
}
